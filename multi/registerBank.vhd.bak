----------------------------------------------------------------------------------
-- Company:   Federal University of Santa Catarina
-- Engineer:  Prof. Dr. Eng. Rafael Luiz Cancian
-- 
-- Create Date:    
-- Design Name: 
-- Module Name:    
-- Project Name: 
-- Target Devices: 
-- Tool versions: 
-- Description: 
--
-- Dependencies: 
--
-- Revision: 
-- Revision 0.01 - File Created
-- Additional Comments: 
--
----------------------------------------------------------------------------------
library IEEE;
use ieee.std_logic_1164.all;
use ieee.numeric_std.all;
use work.UtilFunctions.all;

entity registerBank is 
	generic(	regWidth: integer := 8; -- largura de cada registrador
				regLength: integer := 32); -- quantidade de registradores
	port(	clock, reset, 
			regWrite: in std_logic;  -- se 1 entao vai escrever em registrador
			readReg1, -- num do registrador #1 a ser lido
			readReg2, -- num do registrador #2 a ser lido
			writeReg: in  std_logic_vector(4 downto 0); -- num do registrador a ser escrito
			writeData: in std_logic_vector(regWidth-1 downto 0); -- dado a ser escrito
			readData1, readData2: out std_logic_vector(regWidth-1 downto 0) -- dados lidos dos registradores #1 e #2
	);
end entity;


architecture behavioral of registerBank is 
	type TpRegisterBank is array (0 to regLength-1) of std_logic_vector(regWidth-1 downto 0);
	signal registers, nextRegisters: TpRegisterBank;
begin
	process(clock, reset)
	begin
		if (reset='1') then
         rst: for i in TpRegisterBank'range loop
            registers(i) <= (others=>'0');
         end loop;
		elsif (rising_edge(clock)) then
		   registers <= nextRegisters;
		end if;
	end process;
	wrt: for i in TpRegisterBank'range generate
      nextRegisters(i) <= writeData when regWrite='1' and to_integer(unsigned(writeReg))=i else
                          registers(i); 
	end generate;
   readData1 <= registers(to_integer(unsigned(readReg1)));
   readData2 <= registers(to_integer(unsigned(readReg2)));
end architecture;